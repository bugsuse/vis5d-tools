c Copyright (C) 2000 

c Questo programma è software libero; è lecito ridistribuirlo e/o
c modificarlo secondo i termini della Licenza Pubblica Generica SMR come
c pubblicata da ARPA SMR ; o la versione 1 della licenza o (a scelta)
c una versione successiva.

c Questo programma è distribuito nella speranza che sia utile, ma SENZA
c ALCUNA GARANZIA; senza neppure la garanzia implicita di
c COMMERCIABILITÀ o di APPLICABILITÀ PER UN PARTICOLARE SCOPO. Si veda
c la Licenza Pubblica Generica SMR per avere maggiori dettagli.

c Ognuno dovrebbe avere ricevuto una copia della Licenza Pubblica
c Generica SMR insieme a questo programma; in caso contrario, la si può
c ottenere da Agenzia Regionale Prevenzione e Ambiente (ARPA) Servizio
c Meteorologico Regionale (SMR), Viale Silvani 6, 40122 Bologna, Italia





c	INTEGER GG,MM,AA,ORAI,ORAF
c
c	TYPE*,'DATA'
c	ACCEPT*,GG,MM,AA
c
c	TYPE*,'ORA INIZIO E ORA FINE'
c	ACCEPT*,ORAI,ORAF
c	MINI=0
c	MINF=0
c	SITE_HEIGHT=40.
c	ALAT=44.3
c	ALON=0
c
c	CALL RADMAX(GG,MM,AA,ORAI,MINI,ORAF,MINF,
c	1	ALAT,ALON,SITE_HEIGHT,VALMAX,VALMIN,ier)
c
c	TYPE*,'RADIAZIONE=',VALMAX,VALMIN,IER
c	STOP
c	END
c
c
CC**********************************************************************CC
CC**********************************************************************CC
CC									CC
CC		SERVIZIO METEOROLOGICO REGIONE EMILA ROMAGNA		CC
CC				E.R.S.A.				CC
CC									CC
CC		    UFFICIO INFORMAZIONI E PREVISIONI			CC
CC		    UFFICIO AGRONOMICO					CC
CC	PAOLO PATRUNO							CC
CC		 				POMI LUCA		CC
CC	SELVINI ANDREA							CC
CC						BATTAGLIA FABRIZIO	CC
CC	MARLETTO VITTORIO						CC
CC									CC
CC	BOLOGNA 1990							CC
CC**********************************************************************CC
CC**********************************************************************CC



	SUBROUTINE RADMAX(GG,MM,AA,ORAI,MINI,ORAF,MINF,
	1	ALAT,ALON,SITE_HEIGHT,VALMAX,VALMIN,ier)

COMSTART RADMAX
C	SUBROUTINE RADMAX(GG,MM,AA,ORAI,MINI,ORAF,MINF,
C			ALAT,ALON,SITE_HEIGHT,VALMAX,VALMIN,ier)
C	Valuta la radiazione integrale a cielo sereno in un intervallo
c	di tempo richiesto in funzione della data e della latitudine.
c	Nel periodo che va` dalla mezzanotte a mezza ora prima del sorgere
c	del sole la radiazione viene considerata prossima a zero; cosi` pure
c	nel periodo successivo a mezza ora dopo il tramonto. Se il periodo
c	inizia nell'ora intorno al tramonto o termina nell'ora intorno
c	all'alba viene sempre considerato un periodo che inizia mezza ora
c	prima del tramonto o finisce mezza ora dopo l'alba.
c	Negli altri possibili periodi di misura
c	viene calcolato il valore integrale di radiazione globale.
c	Viene consigliato un intervallo minimo di un'ora.

C	IN:
C		GG,MM,AA	I*4	GIORNO MESE E ANNO
C		ORAI,MINI	I*4	ORA E MINUTI INIZIO MISURA ('Z')
C		ORAF,MINF	I*4	ORA E MINUTI FINE MISURA   ('Z')
C		ALAT		R*4	LATITUDINE SESSAGESIMALE
C		ALON		R*4	LONGITUDINE SESSAGESIMALE
C		SITE_HEIGHT     R*4     ALTEZZA STAZIONE
C	OUT:
C		VALMAX		R*4	RADIAZIONE GLOBALE MAX
c					INTEGRALE IN cal/cm2
C		VALMIN		R*4	RADIAZIONE GLOBALE MAX
c					INTEGRALE IN cal/cm2
C					A CIELO COPERTO
c		IER		I*4	>0 CONDIZIONE DI ERRORE
COMEND

	LOGICAL RIP
	EXTERNAL FCLEAR,FCLOUD
	INTEGER*4 GG,MM,AA,ORAF,MINF,ORAI,MINI
	COMMON /RADIAZIONE/MMC,ALATC,SITE_HEIGHTC,
	1			DELTA,EXTRA_IRRADIANCE,SOLAR_CORRECTION


	PARAMETER FATCONV=0.23892E-4	!da J/m2/giorno a cal/cm2/giorno
	IER=0
	VALMIN1=0.
	VALMAX1=0.

C			per calcolo integrale
		MMC=MM
		ALATC=ALAT
		SITE_HEIGHTC=SITE_HEIGHT

		ERRABS=0.
		ERRREL=0.01
		IRULE=2


	IDATE=NDAYS(GG,MM,AA)-NDAYS(1,1,AA)	!giorno dell'anno
	RLAT=SESS_TO_CENT(ALAT)
	RLON=SESS_TO_CENT(ALON)

	CALL SOLAR_DECLINATION(IDATE,DELTA,EXTRA_IRRADIANCE,SOLAR_CORRECTION)
	CALL ASTRONOMICAL_DAYLENGTH(ALAT,DELTA,DL,ORAL,ORAU)
C	TYPE*,'SORGE',ORAL,'   TRAMONTA',ORAU,'   DURA',DL

	if(oraf.eq.00.and.minf.eq.00)then
		Ioraf=23
		Iminf=59
	ELSE
		ioraf=oraf
		iminf=minf
	end if

	ORASOLF1=IORAF+(RLON*24./360.)+SESS_TO_CENT(IMINF/100.)!ora solare report
	ORASOLI1=ORAI+(RLON*24./360.)+SESS_TO_CENT(MINI/100.)!ora sol. inizio

	IF (ORASOLI1.GE.ORASOLF1)THEN
		IER=1			!errore
		VALMAX=1000.
		VALMIN=0.
		RETURN
	END IF

C						rifaso nelle 24 ore
	IF (ORASOLI1.LT.0)ORASOLI1=24.+ORASOLI1
	IF (ORASOLF1.LT.0)ORASOLF1=24.+ORASOLF1

	IF (ORASOLI1.GT.24)ORASOLI1=ORASOLI1-24.
	IF (ORASOLF1.GT.24)ORASOLF1=ORASOLF1-24.

c						estremi integrazione
	ORASOLI=ORASOLI1
	ORASOLF=ORASOLF1

	RIP=.FALSE.
	IF (ORASOLI1.GE.ORASOLF1)THEN
		RIP=.TRUE.			!faro` due integrazioni
		ORASOLF=23.99			!correggo estremo integrazione
	END IF

33	CONTINUE

	IF (ORASOLF.LT.ORAL-.5.OR.ORASOLI.GT.ORAU+.5) THEN
c	prima della levata o dopo il tramonto

		VALMAX=ORASOLF-ORASOLI

	ELSE
c	dopo la levata prima del tramonto

		ORASOLFX=ORASOLF
		ORASOLIX=ORASOLI

		IF(ORASOLFX.LT.ORAL+.5)then
			ORASOLFX=ORAL+.5	!in prossimita` alba
C			ORASOLIX=ORAL
		END IF
		IF(ORASOLIX.GT.ORAU-.5)THEN
			ORASOLIX=ORAU-.5	!in prossimita` tramonto
C			ORASOLFX=ORAU
		END IF

		IF (ORASOLIX.LT.ORAL)ORASOLIX=ORAL
		IF (ORASOLFX.GT.ORAU)ORASOLFX=ORAU

		CALL QDAG(FCLEAR,ORASOLIX,ORASOLFX,
	1		ERRABS,ERRREL,IRULE,VALMAX,ERREST)
		VALMAX=VALMAX*3600.*FATCONV
		VALMAX=VALMAX+ORASOLF-ORASOLI	!per approssimazione

	END IF 

	IF (ORASOLF.LT.ORAL+.5.OR.ORASOLI.GT.ORAU-.5) THEN
c	prima della levata o dopo il tramonto

		VALMIN=0.	

	ELSE
		ORASOLFN=ORASOLF
		ORASOLIN=ORASOLI

		IF (ORASOLIN.LT.ORAL)ORASOLIN=ORAL
		IF (ORASOLFN.GT.ORAU)ORASOLFN=ORAU
	
		CALL QDAG(FCLOUD,ORASOLIN,ORASOLFN,
	1		ERRABS,ERRREL,IRULE,VALMIN,ERREST)

		VALMIN=VALMIN*3600.*FATCONV
		VALMIN=VALMIN/4.		!precipitazione o nebbia
CC		VALMIN=MAX((VALMIN-5.),0.)	!per approssimazione

	END IF

	IF(RIP)THEN
		RIP=.FALSE.

		ORASOLI=0.			!preparo seconda integrazione
		ORASOLF=ORASOLF1
		VALMIN1=VALMIN			!bufferizzo
		VALMAX1=VALMAX

		GOTO 33				!faccio la seconda
	END IF

	VALMIN=VALMIN1+VALMIN			!sommo la seconda alla prima
	VALMAX=VALMAX1+VALMAX

	RETURN
	END



	FUNCTION FCLEAR(X)
	COMMON /RADIAZIONE/NMESE,SITE_LATITUDE,SITE_HEIGHT,
	1			DELTA,EXTRA_IRRADIANCE,SOLAR_CORRECTION

	CALL CLEARSKY_HORIZONTAL_IRRADIATION
	1	(X,NMESE,SITE_LATITUDE,SITE_HEIGHT,
	1	DELTA,EXTRA_IRRADIANCE,GLOBAL_IRRADIANCE)

	FCLEAR=GLOBAL_IRRADIANCE

	RETURN 
	END

	FUNCTION FCLOUD(X)
	COMMON /RADIAZIONE/NMESE,SITE_LATITUDE,SITE_HEIGHT,
	1			DELTA,EXTRA_IRRADIANCE,SOLAR_CORRECTION

	CALL CLOUDSKY_HORIZONTAL_IRRADIATION
	1	(X,SITE_LATITUDE,DELTA,SOLAR_CORRECTION,OV_IRRADIANCE)

	FCLOUD=OV_IRRADIANCE

	RETURN 
	END


	SUBROUTINE CLEARSKY_HORIZONTAL_IRRADIATION
	1	(T,NMESE,SITE_LATITUDE,SITE_HEIGHT,
	1	DELTA,EXTRA_IRRADIANCE,GLOBAL_IRRADIANCE)


	DIMENSION QLINKE_TURBIDITY_2(12)
	DATA QLINKE_TURBIDITY_2/3.,3.,3.,3.7,3.7,3.8,4.8,4.1,3.2,3.,3.,3./

	HEIGHT_CORRECTION=EXP(-SITE_HEIGHT/8000.)
	SINFIDELTA=SIND(SITE_LATITUDE)*SIND(DELTA)
	COSFIDELTA=COSD(SITE_LATITUDE)*COSD(DELTA)

	SOLAR_HOUR_ANGLE=15*(T-12.)

	SOLAR_ALTITUDE=
	1	ASIND(SINFIDELTA+COSFIDELTA*COSD(SOLAR_HOUR_ANGLE))
	COSPSI=(SIND(SITE_LATITUDE)*SIND(SOLAR_ALTITUDE)-SIND(DELTA))
	1	/(COSD(SITE_LATITUDE)*COSD(SOLAR_ALTITUDE))
	SOLAR_AZIMUTH=-ACOSD(COSPSI)

	IF (SOLAR_ALTITUDE.LE.10.) THEN
	REL_AIR_MASS=HEIGHT_CORRECTION/(SIND(SOLAR_ALTITUDE)+
	1	.15*(SOLAR_ALTITUDE+3.885)**(-1.253))
	ELSE
	REL_AIR_MASS=HEIGHT_CORRECTION/SIND(SOLAR_ALTITUDE)
	END IF

	OPTICAL_THICKNESS=1./(.9*REL_AIR_MASS+9.4)

	IF (QLINKE_TURBIDITY_2(NMESE).LT.2.5) THEN
	QLINKE_FACTOR=QLINKE_TURBIDITY_2(NMESE)-
	1	(.85-2.25*SIND(SOLAR_ALTITUDE)+
	1	1.11*(SIND(SOLAR_ALTITUDE))**2.)*
	1	(QLINKE_TURBIDITY_2(NMESE)-1)/1.5
	ELSE
	QLINKE_FACTOR=QLINKE_TURBIDITY_2(NMESE)-
	1	(.85-2.25*SIND(SOLAR_ALTITUDE)+
	1	1.11*(SIND(SOLAR_ALTITUDE))**2.)
	END IF

	HOURLY_NORMAL_IRRADIANCE=EXTRA_IRRADIANCE*
	1	EXP(-QLINKE_FACTOR*OPTICAL_THICKNESS*REL_AIR_MASS)
	HOURLY_HORIZONTAL_IRRADIANCE=HOURLY_NORMAL_IRRADIANCE*
	1	SIND(SOLAR_ALTITUDE)

	CALL CORRECTION_FACTOR(QLINKE_FACTOR,SOLAR_ALTITUDE,F1)
	CALL TRANSMITTANCE_COEFFICIENT(QLINKE_FACTOR,SOLAR_ALTITUDE,QAM)

	HOURLY_DIFFUSE_IRRADIANCE=.5*F1*(EXTRA_IRRADIANCE*QAM-
	1	HOURLY_NORMAL_IRRADIANCE)*SIND(SOLAR_ALTITUDE)
	GLOBAL_IRRADIANCE=HOURLY_HORIZONTAL_IRRADIANCE+
	1	HOURLY_DIFFUSE_IRRADIANCE
	RETURN
	END


	SUBROUTINE CLOUDSKY_HORIZONTAL_IRRADIATION
	1	(T,SITE_LATITUDE,DELTA,SOLAR_CORRECTION,OV_IRRADIANCE)

	SINFIDELTA=SIND(SITE_LATITUDE)*SIND(DELTA)
	COSFIDELTA=COSD(SITE_LATITUDE)*COSD(DELTA)

	SOLAR_HOUR_ANGLE=15*(T-12.)

	SOLAR_ALTITUDE=
	1	ASIND(SINFIDELTA+COSFIDELTA*COSD(SOLAR_HOUR_ANGLE))

	OV_IRRADIANCE=SOLAR_CORRECTION*(2.6+182.6*SIND(SOLAR_ALTITUDE))

	RETURN
	END
	SUBROUTINE ASTRONOMICAL_DAYLENGTH(SITE_LATITUDE,DELTA,S0,TR,TS)

	S0=ACOSD(-TAND(DELTA)*TAND(SITE_LATITUDE))/7.5
	TR=12.-S0/2.
	TS=12.+S0/2.
	RETURN
	END



	SUBROUTINE CORRECTION_FACTOR(QLINKE_FACTOR,SOLAR_ALTITUDE,F1)
	DIMENSION A(6),B(6)
	DATA A/9.272E-1,1.85E-2,-5.377E-4,5.512E-6,-1.502E-8,-3.816E-11/
	DATA B/-1.9043E-1,1.8226E-2,-6.0133E-4,
	1	1.1015E-5,-1.0043E-7,3.5385E-10/
	FIRST=0.
	SECOND=0.
	DO I=1,6
	 FIRST=FIRST+A(I)*SOLAR_ALTITUDE**(I-1)
	 SECOND=SECOND+B(I)*SOLAR_ALTITUDE**(I-1)
	END DO
	F1=FIRST+SECOND*(QLINKE_FACTOR-5)
	RETURN
	END

	SUBROUTINE TRANSMITTANCE_COEFFICIENT(QLINKE_FACTOR,SOLAR_ALTITUDE,QAM)
	DIMENSION A(6)
	DATA A/1.294,2.4417E-2,-3.973E-4,3.8034E-6,-2.2145E-8,5.8332E-11/
	QAM=0.
	DO I=1,6
	QAM=QAM+A(I)*SOLAR_ALTITUDE**(I-1)
	END DO
	QAM=QAM*(.506-.010788*QLINKE_FACTOR)
	RETURN
	END


	SUBROUTINE SOLAR_DECLINATION(J,DELTA,EXTRA_IRRADIANCE,
	1	SOLAR_CORRECTION)
	DJ=360*J/365.25
	DELTA=ASIND(.3978*SIND(DJ-80.2+1.92*SIND(DJ-2.8)))
	SOLAR_CORRECTION=(1.+.003344*COSD(DJ-2.8))
	EXTRA_IRRADIANCE=1367.*SOLAR_CORRECTION
	RETURN
	END

